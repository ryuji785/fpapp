plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.flywaydb.flyway' version '10.14.0'
    id 'java'
}

group = 'app.fp'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    create("flyway")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'org.flywaydb:flyway-core:10.14.0'
    implementation 'org.flywaydb:flyway-database-postgresql:10.14.0'
    runtimeOnly 'org.postgresql:postgresql:42.7.2'

    add('flyway', 'org.flywaydb:flyway-database-postgresql:10.14.0')
    add('flyway', 'org.postgresql:postgresql:42.7.2')

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
    url = System.getenv("DB_URL")
    user = System.getenv("DB_USER")
    password = System.getenv("DB_PASS_WORD")
    schemas = ["public"]
    locations = ["classpath:db/migration"]
    cleanDisabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootRun') {
    systemProperty "spring.datasource.url", System.getenv("DB_URL")
    systemProperty "spring.datasource.username", System.getenv("DB_USER")
    systemProperty "spring.datasource.password", System.getenv("DB_PASS_WORD")
}

