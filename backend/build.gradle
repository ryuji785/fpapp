buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.cdimascio:dotenv-java:3.0.0'
    }
}

import io.github.cdimascio.dotenv.Dotenv

plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'org.flywaydb.flyway' version '9.22.3'
}

group = 'app.fp'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'org.flywaydb:flyway-core:10.17.2'
    implementation 'org.flywaydb:flyway-database-postgresql:10.17.2'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def dotenv = Dotenv.configure()
        .directory(rootProject.rootDir.toString())
        .ignoreIfMissing()
        .load()

flyway {
    url = 'jdbc:postgresql://localhost:5432/fpapp'
    user = dotenv.get('DB_USER') ?: System.getenv('DB_USER')
    password = dotenv.get('DB_PASS_WORD') ?: System.getenv('DB_PASS_WORD')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

tasks.named('test') {
    useJUnitPlatform()
}
